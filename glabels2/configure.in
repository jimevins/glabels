dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)

AC_INIT(glabels, 2.1.4, http://sourceforge.net/tracker/?func=add&group_id=46122&atid=445116)
AC_CONFIG_SRCDIR(src/glabels.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

AC_PROG_INTLTOOL([0.21])

AM_PROG_LIBTOOL

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

GNOME_COMPILE_WARNINGS

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

GTK_DOC_CHECK(1.0)

dnl ---------------------------------------------------------------------------
dnl - LIBGLABELS API versioning
dnl ---------------------------------------------------------------------------
dnl From the libtool manual:
dnl 1. Start with version information of `0:0:0' for each libtool library.
dnl 2. Update the version information only immediately before a public release.
dnl    More frequent updates are unnecessary, and only guarantee that the current
dnl    interface number gets larger faster.
dnl 3. If the library source code has changed at all since the last update, then increment
dnl    revision (`c:r:a' becomes `c:r+1:a').
dnl 4. If any interfaces have been added, removed, or changed since the last update,
dnl    increment current, and set revision to 0.
dnl 5. If any interfaces have been added since the last public release, then increment age.
dnl 6. If any interfaces have been removed since the last public release, then set age
dnl    to 0.
LIBGLABELS_C=4
LIBGLABELS_R=0
LIBGLABELS_A=0

LIBGLABELS_VERSION_INFO=${LIBGLABELS_C}:${LIBGLABELS_R}:${LIBGLABELS_A}
AC_SUBST(LIBGLABELS_VERSION_INFO)


dnl ---------------------------------------------------------------------------
dnl - Library dependencies
dnl ---------------------------------------------------------------------------
GLIB_REQUIRED=2.12.0
GTK_REQUIRED=2.10.0
LIBGNOME_REQUIRED=2.16.0
LIBGNOMEUI_REQUIRED=2.16.0
LIBXML_REQUIRED=2.6.0
LIBGLADE_REQUIRED=2.6.0
LIBEBOOK_REQUIRED=1.8.0

dnl Make above strings available for packaging files (e.g. rpm spec files)
AC_SUBST(GLIB_REQUIRED)
AC_SUBST(GTK_REQUIRED)
AC_SUBST(LIBGNOME_REQUIRED)
AC_SUBST(LIBGNOMEUI_REQUIRED)
AC_SUBST(LIBXML_REQUIRED)
AC_SUBST(LIBGLADE_REQUIRED)
AC_SUBST(LIBEBOOK_REQUIRED)


dnl ---------------------------------------------------------------------------
dnl - Check for evolution data server
dnl ---------------------------------------------------------------------------
AC_ARG_WITH(libebook,
	    [AC_HELP_STRING([--without-libebook],
                            [build without Evolution Data Server support])])
have_libebook=no
if test "x$with_libebook" != xno; then
	PKG_CHECK_MODULES(LIBEBOOK, libebook-1.2 >= $LIBEBOOK_REQUIRED,
			  [have_libebook=yes], [have_libebook=no])
fi

if test "x$have_libebook" = "xyes"; then
	AC_DEFINE(HAVE_LIBEBOOK,1,[Define to 1 for EDS support])
	OPTIONAL_MODULES="$OPTIONAL_MODULES libebook-1.2 >= $LIBEBOOK_REQUIRED"
fi


dnl ---------------------------------------------------------------------------
dnl - GLABELS prerequisites
dnl ---------------------------------------------------------------------------
PKG_CHECK_MODULES(GLABELS, \
glib-2.0 >= $GLIB_REQUIRED \
gtk+-2.0 >= $GTK_REQUIRED \
libgnome-2.0 >= $LIBGNOME_REQUIRED \
libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED \
libxml-2.0 >= $LIBXML_REQUIRED \
libglade-2.0 >= $LIBGLADE_REQUIRED \
$OPTIONAL_MODULES \
)

AC_SUBST(GLABELS_CFLAGS)
AC_SUBST(GLABELS_LIBS)


dnl ---------------------------------------------------------------------------
dnl - LIBGLABELS more modest prerequisites
dnl ---------------------------------------------------------------------------
PKG_CHECK_MODULES(LIBGLABELS, \
glib-2.0 >= $GLIB_REQUIRED \
libxml-2.0 >= $LIBXML_REQUIRED \
)

AC_SUBST(LIBGLABELS_CFLAGS)
AC_SUBST(LIBGLABELS_LIBS)


dnl ---------------------------------------------------------------------------
dnl - Enable deprecation testing
dnl ---------------------------------------------------------------------------
AC_ARG_ENABLE(deprecations,
              [AC_HELP_STRING([--enable-deprecations],
                              [warn about deprecated usages [default=no]])],,
              [enable_deprecations=no])

if test "x$enable_deprecations" = "xyes"; then
   DISABLE_DEPRECATED_CFLAGS="\
-DG_DISABLE_DEPRECATED \
-DGDK_DISABLE_DEPRECATED \
-DGTK_DISABLE_DEPRECATED \
-DGDK_PIXBUF_DISABLE_DEPRECATED \
-DGNOME_DISABLE_DEPRECATED"
   AC_SUBST(DISABLE_DEPRECATED_CFLAGS)
fi


dnl ---------------------------------------------------------------------------
dnl - Is freedesktop mime dbase available?
dnl ---------------------------------------------------------------------------
AC_PATH_PROG(UPDATE_MIME_DATABASE, update-mime-database, no)
AM_CONDITIONAL(HAVE_NEW_MIME, test x$UPDATE_MIME_DATABASE != xno)

AC_PATH_PROG(UPDATE_DESKTOP_DATABASE, update-desktop-database, no)
AM_CONDITIONAL(HAVE_NEWER_MIME, test x$UPDATE_DESKTOP_DATABASE != xno)

AC_ARG_ENABLE(update-mimedb, AC_HELP_STRING([--disable-update-mimedb],
              [disable the update-mime-database after install [default=no]]),,
              enable_update_mimedb=yes)
AM_CONDITIONAL(ENABLE_UPDATE_MIMEDB, test x$enable_update_mimedb = xyes)

AC_ARG_ENABLE(update-desktopdb, AC_HELP_STRING([--disable-update-desktopdb],
              [disable the update-desktop-database after install [default=no]]),,
              enable_update_desktopdb=yes)
AM_CONDITIONAL(ENABLE_UPDATE_DESKTOPDB, test x$enable_update_desktopdb = xyes)


dnl ---------------------------------------------------------------------------
dnl - Supported languages
dnl ---------------------------------------------------------------------------
GETTEXT_PACKAGE=glabels
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Gettext package])

dnl Add the languages here.
ALL_LINGUAS="fr de ja pt_BR es ru zh_TW.Big5 pl cs it eo"
AM_GLIB_GNU_GETTEXT


dnl ---------------------------------------------------------------------------
dnl - Makefiles, etc.
dnl ---------------------------------------------------------------------------
AC_OUTPUT([
Makefile
barcode-0.98/Makefile
iec16022-0.2.1/Makefile
libglabels/Makefile
libglabels/libglabels.pc
src/Makefile
src/pixmaps/Makefile
src/stock-pixmaps/Makefile
src/mygal/Makefile
data/Makefile
data/templates/Makefile
data/pixmaps/Makefile
data/mime/Makefile
data/glade/Makefile
data/desktop/Makefile
data/dtd/Makefile
data/man/Makefile
po/Makefile.in
help/Makefile
help/C/Makefile
docs/Makefile
docs/libglabels/Makefile
glabels.spec
])


dnl ---------------------------------------------------------------------------
dnl - Print configuration information
dnl ---------------------------------------------------------------------------
echo "
Configuration:

	Package:                        ${PACKAGE}-${VERSION}:
        Installation prefix             ${prefix}
        Source code location:           ${srcdir}
        Compiler:                       ${CC} 
	Evolution Data Server support:  ${have_libebook}

"

cat ${srcdir}/MESSAGE-OF-DOOM

